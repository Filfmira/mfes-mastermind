class Jogo
types
	public String = Utilities`String;
	
values
-- TODO Define values here

instance variables
	private tabuleiros : map String to Tabuleiro; -- um para cada equipa
	inv card (dom tabuleiros) = 2;
	
operations
	--Construtor que recebe um map equipa->chave e um campeonato e retorna o jogo criado
	public Jogo : map String to seq1 of Cor`Cor * Campeonato ==> Jogo
	Jogo (chaves, campeonato) == (
		dcl domMapSeq : seq of String := [];
		for all x in set dom chaves do
			domMapSeq := domMapSeq ^ [x];
			tabuleiros := {domMapSeq(1) |-> new Tabuleiro(chaves(domMapSeq(2))),
				domMapSeq(2) |-> new Tabuleiro(chaves(domMapSeq(1)))};
		)
	pre card (dom chaves) = 2 and
		forall jogador in set dom chaves &
			jogador in set campeonato.getJogadores() and
		forall chave in set rng chaves &
			len chave = Tabuleiro`comprimentoChave
	post forall jogador in set (dom tabuleiros) &
		let adversario = dom chaves \ {jogador} in
			{tabuleiros(jogador).getChave()} = rng (adversario <: chaves);
			--chave do tabuleiro da  minha equipa == chave gerada pelo adversario


	-- Construtor que recebe um map com o nome da equipa -> Tabuleiro e retorna o Jogo criado
	public Jogo : map String to Tabuleiro ==> Jogo
	Jogo (tabuleirosP) ==
		tabuleiros := tabuleirosP
	pre card (dom tabuleirosP) = 2
	post tabuleiros = tabuleirosP;

	--Retorna o map jogador->tabuleiro
	public getTabuleiros : () ==> map String to Tabuleiro
	getTabuleiros () == return tabuleiros
	post RESULT = tabuleiros;

	--Retorna o set de jogadores de um jogo
	pure public getJogadores : () ==> set of String
	getJogadores () == return dom tabuleiros
	post RESULT = dom tabuleiros;

	-- Retorna o tabuleiro do jogador
	public getTabuleiroJogador : String ==> Tabuleiro
	getTabuleiroJogador (jogador) == return tabuleiros(jogador)
	post RESULT = tabuleiros(jogador);

	-- Devolve o Tabuleiro do jogador adversário
	public getTabuleiroAdversario : String ==> Tabuleiro
	getTabuleiroAdversario (jogador) ==
		let adversario in set (dom tabuleiros)\{jogador}
		in
		return tabuleiros(adversario)
	pre jogador in set (dom tabuleiros)

	post RESULT in set rng tabuleiros
		and {RESULT} = rng ({jogador} <-: tabuleiros);
		
		
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end Jogo